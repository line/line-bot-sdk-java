/*
 * Copyright 2016 LINE Corporation
 *
 * LINE Corporation licenses this file to you under the Apache License,
 * version 2.0 (the "License"); you may not use this file except in compliance
 * with the License. You may obtain a copy of the License at:
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
 * WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
 * License for the specific language governing permissions and limitations
 * under the License.
 */

// ./gradlew clean && ./gradlew uploadArchives -Prelease

buildscript {
    ext.kotlin_version = '1.3.11'

    repositories {
        mavenCentral()
        maven { url 'https://plugins.gradle.org/m2/' }
        maven { url 'https://repo.spring.io/plugins-release' }
    }
    dependencies {
        classpath 'gradle.plugin.com.github.spotbugs:spotbugs-gradle-plugin:1.6.6'
        classpath 'gradle.plugin.com.gorylenko.gradle-git-properties:gradle-git-properties:1.5.2'
        classpath 'io.spring.gradle:dependency-management-plugin:1.0.6.RELEASE'
        classpath 'io.spring.gradle:propdeps-plugin:0.0.10.RELEASE'
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:2.1.0.RELEASE'
        classpath 'io.franzbecker:gradle-lombok:1.14'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.puppycrawl.tools:checkstyle:8.15'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.20.0'
    }
}

apply plugin: 'idea'
apply plugin: 'jacoco'
apply plugin: 'com.github.ben-manes.versions'

group = 'com.linecorp.bot'
version = '2.3.0'

//set build variables based on build type (release, continuous integration, development)
def isDevBuild
def isReleaseBuild
def sonatypeRepositoryUrl
if (hasProperty('release')) {
    isReleaseBuild = true
    sonatypeRepositoryUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
} else if (hasProperty('ci')) {
    version += '-SNAPSHOT'
    sonatypeRepositoryUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
} else {
    isDevBuild = true
    version += '-SNAPSHOT'
}

allprojects {
    repositories {
        mavenCentral();
    }
}

subprojects {
    apply plugin: 'com.github.spotbugs'
    apply plugin: 'propdeps'
    apply plugin: 'propdeps-maven'
    apply plugin: 'propdeps-idea'
    apply plugin: 'propdeps-eclipse'
    apply plugin: 'java'
    apply plugin: 'checkstyle'
    apply plugin: 'io.franzbecker.gradle-lombok'
    if (new File("${project.rootDir}/.git").exists()) {
        logger.info("There's .git directory. Create git.properties.")
        apply plugin: 'com.gorylenko.gradle-git-properties'
    } else {
        logger.warn("There's no .git directory. Can't create git.properties.");
    }
    apply plugin: 'io.spring.dependency-management'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    group = rootProject.group
    version = rootProject.version

    ext['guava.version'] = '27.0.1-jre'
    ext['okhttp3.version'] = '3.12.0'
    ext['retrofit.version'] = '2.5.0'

    dependencyManagement {
        if (project.name != 'test-boot1-compatibility') {
            imports {
                mavenBom 'io.spring.platform:platform-bom:Cairo-RELEASE'
                mavenBom 'org.springframework.boot:spring-boot-dependencies:2.1.1.RELEASE'
            }
        }

        dependencies {
            dependency 'com.github.stefanbirkner:system-rules:1.19.0'
            dependency 'com.squareup.okhttp3:logging-interceptor:' + ext['okhttp3.version']
            dependency 'com.squareup.okhttp3:mockwebserver:' + ext['okhttp3.version']
            dependencySet(group: 'com.squareup.retrofit2', version: ext['retrofit.version']) {
                entry 'converter-jackson'
                entry 'retrofit'
            }
            dependency 'org.assertj:assertj-core:3.11.1'
            dependency 'org.projectlombok:lombok:1.18.4'
        }
    }

    dependencies {
        compileOnly 'org.springframework.boot:spring-boot-configuration-processor'
        // http://docs.spring.io/spring-boot/docs/current/reference/html/configuration-metadata.html#configuration-metadata-annotation-processor

        testCompile 'com.google.guava:guava'
        testCompile 'com.github.stefanbirkner:system-rules'
        testCompile 'com.squareup.okhttp3:mockwebserver'
        testCompile 'org.hibernate:hibernate-validator'
        testCompile 'org.springframework.boot:spring-boot-starter-test' // MockHttpServletRequest
        testCompile 'org.springframework.boot:spring-boot-starter-logging'
    }

    compileJava.dependsOn(processResources)
    // http://docs.spring.io/spring-boot/docs/current/reference/html/configuration-metadata.html#configuration-metadata-annotation-processor

    def springManagedModules = [
        'org.springframework',
        'com.fasterxml.jackson',
        'javax.validation',
        'org.hibernate',
        'javax.servlet'
    ];
    dependencyUpdates.resolutionStrategy {
        componentSelection { rules ->
            rules.all { ComponentSelection selection ->
                boolean springManaged = springManagedModules.any { module ->
                    selection.candidate.group.startsWith(module)
                }
                if (springManaged) {
                    selection.reject('Spring Managed')
                }
                boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                    selection.candidate.version ==~ /(?i).*[.-]${qualifier}\d*[.\d-]*/
                }
                if (rejected) {
                    selection.reject('Release candidate')
                }
            }
        }
    }

    jar {
        manifest {
            attributes 'Implementation-Title': project.name,
                'Implementation-Version': version
        }
    }

    lombok {
        version = '1.18.4'
    }

    if (!project.name.startsWith('sample-') && !project.name.startsWith('test-')) {
        task delombok(type: io.franzbecker.gradle.lombok.task.DelombokTask, dependsOn: compileJava) {
            ext.outputDir = file("$buildDir/delombok")
            outputs.dir(outputDir)
            sourceSets.main.java.srcDirs.each {
                inputs.dir(it)
                args(it, "-d", outputDir)
            }
        }

        javadoc {
            dependsOn delombok
            source = delombok.outputDir
            options.locale = 'en_US'
            options.addStringOption('Xdoclint:none', '-quiet')
            // To create javadoc for generated method&constructor, delombok & run javadoc on delombok.outputDir.
        }

        task javadocJar(type: Jar, dependsOn: 'javadoc') {
            classifier = 'javadoc'
            from javadoc.destinationDir
        }

        task sourcesJar(type: Jar, dependsOn: 'classes') {
            classifier = 'sources'
            from sourceSets.main.allSource
        }

        artifacts {
            archives javadocJar, sourcesJar
        }
    }

    checkstyle {
        configProperties = [projectDir: rootProject.projectDir]
        toolVersion = '8.15'
    }

    spotbugs {
        excludeFilter = file("$rootProject.projectDir/config/findbugs/excludeFilter.xml")
    }

    compileJava {
        options.compilerArgs << '-Xlint:all' << '-Xlint:deprecation' << '-Werror' << '-Xlint:-processing' << '-parameters'
    }

    project.plugins.withType(org.springframework.boot.gradle.plugin.SpringBootPlugin) {
        bootRun {
            systemProperties System.properties
        }
    }
}

task codeCoverageReport(type: JacocoReport) {
    onlyIf = { true }
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    [':line-bot-api-client', ':line-bot-model', ':line-bot-servlet', ':line-bot-spring-boot', ':line-bot-cli'].each {
        sourceSets(project(it).sourceSets.main)
    }

    reports {
        xml.enabled true
        xml.destination file('reports/jacoco/report.xml')
        html.enabled true
        csv.enabled false
    }
}

[':line-bot-api-client', ':line-bot-model', ':line-bot-servlet', ':line-bot-spring-boot', ':line-bot-cli'].each { projectName ->
    project(projectName) { project ->
        apply plugin: 'jacoco'
        apply plugin: 'signing'
        apply plugin: 'maven'

        signing {
            required { isReleaseBuild }
            sign configurations.archives
        }

        uploadArchives {
            repositories {
                if (isDevBuild) {
                    mavenLocal()
                } else {
                    mavenDeployer {
                        if (isReleaseBuild) {
                            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
                        }

                        repository(url: sonatypeRepositoryUrl) {
                            authentication(userName: sonatypeUsername, password: sonatypePassword)
                        }

                        pom.project {
                            name project.name
                            packaging 'jar'
                            description project.description
                            url 'https://github.com/line/line-bot-sdk-java'

                            scm {
                                url 'scm:git@github.com:line/line-bot-sdk-java.git'
                                connection 'scm:git@github.com:line/line-bot-sdk-java.git'
                                developerConnection 'scm:git@github.com:line/line-bot-sdk-java.git'
                            }
                            licenses {
                                license {
                                    name 'Apache'
                                    url 'https://opensource.org/licenses/Apache-2.0'
                                }
                            }
                            developers {
                                developer {
                                    id 'tokuhirom'
                                    name 'Tokuhiro Matsuno'
                                    email 'tokuhirom@gmail.com'
                                }
                                developer {
                                    id 'kazuki-ma'
                                    name 'Kazuki MATSUDA'
                                    email 'matsuda.kazuki@gmail.com'
                                }
                            }
                        }
                    }
                }
            }
        }
    }
}
